    alias InputFlip d0
    alias Machine d1
    alias CIdSelector d2
    alias IdSelector d3
    alias ActionSelector d4
    alias Id r15
    alias Action r14
    alias State r13
    alias Ret r12
    alias CId r11
    define Idle 0
    define Selected 1
    define Deselected 2
    define Emptying 3
init:
    jal close
    jal routeOut
    jal getId
    move State Idle
start:
    yield
	seq r0 State Selected
	s db Setting r0
    beqal State Selected routeIn
    bneal State Selected routeOut
	jal fetch
    bne CId Id start
    beqal Action Selected selfSelect
    beqal Action Deselected selfDeselect
    beqal Action Emptying empty
    j start
# --High level functions--
# Select
selfSelect:
    push ra
    move State Selected
    jal routeIn
    s ActionSelector Setting Idle
	s db Setting 1
    pop ra
    j ra
# Deselect
selfDeselect:
    push ra
	s db Setting 0
    move State Deselected
    jal routeOut
    s ActionSelector Setting Idle
    pop ra
    j ra
# Empty
empty:
    push ra
	s db Setting 0
    move State Emptying
    jal routeOut
    jal open
emptyLoop:
    jal contents
    bnez Ret emptyLoop
    move State Idle
    s ActionSelector Setting Idle
    jal close
    pop ra
    j ra
# --Low level functions--
open:
    s Machine Open 1
	sleep 1
    j ra
close:
    s Machine Open 0
	sleep 1
    j ra
routeIn:
    s InputFlip Mode 1
    s InputFlip Setting 1
    s InputFlip SettingOutput 0
    j ra
routeOut:
    s InputFlip Mode 0
    s InputFlip Setting 0
    s InputFlip SettingOutput 1
    j ra
fetch:
    l Action ActionSelector Setting
    l Id IdSelector Setting
    j ra
getId:
    l CId CIdSelector Setting
    j ra
contents:
    l Ret Machine Reagents
    j ra