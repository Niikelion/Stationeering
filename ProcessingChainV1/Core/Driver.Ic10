    # Processing Chain V1 Driver
    alias IdSelector d0
    alias ActionSelector d1
    alias IdRequester d2
    alias Id r15
    alias Action r14
    alias LastId r13
    define IdleState 0
    define SelectState 1
    define DeselectState 2
    define EmptyState 3
init:
    move LastId -1
    # Erase current command
    s IdSelector Setting -1
    s ActionSelector Setting 0
start:
    yield
    # Wait for id request
    l r0 IdRequester Setting
    bltz r0 start
    # Process request
    move Id r0
    jal transfer
    # Signal end of request
	s IdRequester Setting -1
    j start
# --Functions--
# Execute command
execute:
    yield
    # Wait for end of previous action
    l r0 ActionSelector Setting
    bnez r0 execute
    # Send action
    s IdSelector Setting Id
    s ActionSelector Setting Action
execWait:
    # Wait for action to end
    yield
    l r0 ActionSelector Setting
    bnez r0 execWait
    j ra
# Transfer items
transfer:
    push ra
    # Select requested device
    move Action SelectState
    jal execute
    # Exit if does not have to empty previous device
    bltz LastId transferEnd
    beq LastId Id transferEnd
    push Id
    # Empty previous device
    move Id LastId
    move Action DeselectState
    jal execute
    move Action EmptyState
    jal execute
    pop Id
transferEnd:
    # Save last device and signal current device
    move LastId Id
	s db Setting Id
    pop ra
    j ra